Use cWebView.pkg
Use cWebPanel.pkg
Use cWebForm.pkg 
Use cWebGroup.pkg
Use cWebMenuGroup.pkg 
Use cWebMenuItem.pkg
Use cWebLabel.pkg

Use cEyeDropperAPI.pkg
Use cWebButton.pkg

Object oEyeDropperDemo is a cWebView
    Set peWebViewStyle to wvsDrillDown
    Set peViewType to vtUndefined
    Set pbShowCaption to False
    Set Verify_Save_msg to 0 // don't confirm saves
    
    Set psCaption to "EyeDropper Demo"

    Set piMaxWidth to 1024
    Set piColumnCount to 12
    Set pbServerOnShow to True
    
    Object oEyeDropperAPI is a cEyeDropperAPI
        Procedure OnSelect String sRGBHex
            WebSet psBackgroundColor of oWebColorPicked to sRGBHex
            WebSet psCaption of oWebColorPicked to ("You picked the color " + sRGBHex)
        End_Procedure
    End_Object

    Object oWebMainPanel is a cWebPanel
        Set piColumnCount to 12
        WebSetResponsive piColumnCount rmMobile to 6

        Object oWebLabel is a cWebLabel
            Set psCaption to "Supported?"
            Set piColumnSpan to 12
        End_Object

        Object oWebButton is a cWebButton
            Set piColumnSpan to 0
            Set psCaption to "Pick a color"
        
            Procedure OnClick
                Send Open of oEyeDropperAPI
            End_Procedure
        End_Object

        Object oWebColorPicked is a cWebLabel
            Set psCaption to "You did not pick a color yet"
            Set peAlign to alignCenter
            Set piColumnSpan to 12
        End_Object
        
    End_Object
    
    Procedure OnShow
        Boolean bIsSupported
        
        Forward Send OnShow

        WebGet pbIsSupported of oEyeDropperAPI to bIsSupported
        If (bIsSupported) Begin
            WebSet psCaption of oWebLabel to "EyeDropper API is supported"
            WebSet psTextColor of oWebLabel to "green"
        End
        Else Begin
            WebSet psCaption of oWebLabel to "EyeDropper API is NOT supported (try Chrome, Edge or Opera on desktop)"
            WebSet psTextColor of oWebLabel to "red"
        End
    End_Procedure
End_Object
